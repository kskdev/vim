" How to use this file.
" このファイルを .vimrc として，HOMEディレクトリに配置して，vimを起動するだけ．
" Installer for Windows: ./install_cmd_alone.bat
" Installer for MacOS/Linux: ./install_vim_zsh.sh
"
" Gvim on Windows: _gvimrc _vimrc にリネームしてホームディレクトリに配置.
" _gvimrc or _vimrc だけだとカラースキーマが反映されない
" TODO 現状FZFがうまく機能していない
" 外部依存のPython を削除してほぼvimscriptで完結するようにプラグインを再考
" せいぜい,FZFで利用するバイナリファイル程度
" TODO 本当にPythonの依存から脱却できているかは微妙.
"      (vim-lspは使っているかもしれないため...)

" //		vim setting and tips		//
" vim起動時にReplace Modeで起動されてしまう現象の対策
" 詳しくは : https://github.com/vim-jp/issues/issues/578
set t_u7=
set t_RV=
" Backspaceで何でも文字を削除できるように設定
set backspace=start,eol,indent
" バッファの文字コードの設定
set encoding=utf-8
" 設定した候補から自動で文字コードを設定
set fileencodings=utf-8,iso-2022-jp,cp932,sjis,euc-jp
"ファイルフォーマットをOSから自動で設定
set fileformats=unix,dos,mac
" スワップファイル不要
set noswapfile
" バックアップファイル不要
set nobackup
" undoファイル不要
set noundofile
" 背景の透過処理
highlight Normal ctermbg=none
" □ や○ 文字が崩れ問題を解消
set ambiwidth=double
" Concealの無効化(マルチバイト文字の修飾表現)
let g:tex_conceal = ''
" ファイル展開時,カーソル位置復元
if has("autocmd")
    au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
endif

"::::::::::::::::::::::::::::::::::
"::::::::::display setting
" 行数を表示
set number
" ステータスラインに行数を表示
set ruler
" カーソル業をハイライト
set cursorline
" 対応する括弧をハイライト
set showmatch
" showmatchのハイライト時間を調整(1 is 0.1sec)
set matchtime=1
" 入力中のコマンドを表示
set showcmd
" 現在のコマンドモードを非表示
set noshowmode
" コマンドラインの行数
set cmdheight=1
" N行余裕を持たせてスクロール
set scrolloff=3
" ステータスラインを常に表示
set laststatus=2
" エディタの編集タイトルを表示しない
set notitle
" 左端2文字分の空間を常に表示
set signcolumn=yes

":::::::::::::::::::::::::::::::::::::
"::::::::::search setting
" 検索結果のハイライト表示
set hlsearch
" 大文字小文字無視して検索
set ignorecase
" 入力文字数を増やすと候補が絞られる
set incsearch
" Escで検索ハイライトを削除
nnoremap <silent><ESC> :nohlsearch<CR>

"::::::::::::::::::::::::::::::::::::::
"::::::::::indent setting
" Tabを押した時，Tab文字を使わず半角スペースを使う
set expandtab
" タブ文字の幅の設定
set tabstop=4
" 自動インデントの幅
set shiftwidth=4
" 改行時に行からインデントの増減を行う．
set smartindent
" 言語別設定
filetype on
filetype indent on
augroup fileTypeIndent
    autocmd!
    au BufNewFile,BufRead *.py setlocal tabstop=4 softtabstop=4 shiftwidth=4
    au BufNewFile,BufRead *.zsh setlocal tabstop=2 softtabstop=2 shiftwidth=2
    au BufNewFile,BufRead *.sh setlocal tabstop=2 softtabstop=2 shiftwidth=2
    au BufNewFile,BufRead *.vim setlocal tabstop=2 softtabstop=2 shiftwidth=2
    au BufNewFile,BufRead *.tex setlocal tabstop=2 softtabstop=2 shiftwidth=2
    au BufNewFile,BufRead *.yaml setlocal tabstop=2 softtabstop=2 shiftwidth=2
augroup END

"::::::::::::::::::::::::::::::::::::::
"::::::::::autoComplete setting
" CLモードでTabキーによるファイル名補完を有効化
set wildmenu
" 大文字小文字を無視
set wildignorecase
" 補完一覧を表示
set wildmode=list,full
" 補完候補が1つでもポップアップを表示
set completeopt=menuone
" 補完候補のポップアップ数
set pumheight=12

"::::::::::::::::::::::::::::::::::::::
"::::::::::Key Map
" Leaderキーの変更 from ',' to <Space>
let mapleader = "\<Space>"

"  vimのファイルパス補完のマッピングを変更
inoremap <C-x><C-f> <C-X><C-F><C-P>

" ノーマルモード時だけ ; と : を入れ替える(US配列に打ちやすさを考慮)
" ただし,検索リピートの邪魔になるので現在は無効化
" nnoremap ;; :

" インサートモードでのカーソル挙動
inoremap <C-l> <Right>

" 連続入力が必要なコマンドの入力受付時間[ms]
set timeoutlen=500
" ESCを押してからノーマルモードに移行するまでの待機時間[ms]
set ttimeoutlen=10

" 画面切り替え(Ctrl+wを2回も押すのは面倒)
nnoremap <C-w> <C-w><C-w>

" 左右のカーソルh,lで行間も移動可能にする(h,lの追加は非推奨らしい...)
set whichwrap=b,s,<,>,[,],h,l

" インサートモードからノーマルモードへ移行
" (Linux+fcitxの場合は日本語入力を無効化してノーマルモードに移行)
if has('unix')
    inoremap jj <ESC>:call system('fcitx-remote -c')<CR>
    inoremap っｊ <ESC>:call system('fcitx-remote -c')<CR>
else
    inoremap jj <ESC>
    inoremap っｊ <ESC>
endif



""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"  /$$$$$$$            /$$
"  | $$__  $$          |__/
"  | $$  \ $$  /$$$$$$  /$$ /$$$$$$$
"  | $$  | $$ /$$__  $$| $$| $$__  $$
"  | $$  | $$| $$$$$$$$| $$| $$  \ $$
"  | $$  | $$| $$_____/| $$| $$  | $$
"  | $$$$$$$/|  $$$$$$$| $$| $$  | $$
"  |_______/  \_______/|__/|__/  |__/
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" viとの互換を切る
if &compatible 
    set nocompatible 
endif

" dein用ルートディレクトリ
let g:dein_dir = expand('~/.vim/dein')
" dein.vimの実体があるディレクトリをセット
let s:dein_repo_dir = g:dein_dir . '/repos/github.com/Shougo/dein.vim'
" dein.vimが存在していない場合はgithubからclone
if &runtimepath !~# '/dein.vim'
    if !isdirectory(s:dein_repo_dir)
        execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
    endif
    execute 'set runtimepath^=' . s:dein_repo_dir
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""
" if dein#load_state(g:dein_dir)
call dein#begin(g:dein_dir)
call dein#add(s:dein_repo_dir) " dein.vim 本体

" Language-Server-Protocol
call dein#add('prabirshrestha/vim-lsp') " LSP本体
call dein#add('mattn/vim-lsp-settings') " Language Server の簡単インストール
call dein#add('liuchengxu/vista.vim') " LSP経由でタグジャンプなどを可能にする

" 補完形
call dein#add('Shougo/echodoc.vim') " 関数等の引数を表示
call dein#add('Shougo/neosnippet-snippets') " スニペット集
call dein#add('Shougo/neosnippet.vim') " スニペット補完の動作定義
call dein#add('prabirshrestha/asyncomplete.vim')  " asyncomplete.vim 本体
" Asyncomplete 拡張
call dein#add('prabirshrestha/asyncomplete-lsp.vim')  " LSP経由で補完候補に追加
call dein#add('prabirshrestha/asyncomplete-buffer.vim')  " バッファを補完候補に追加
call dein#add('prabirshrestha/asyncomplete-file.vim')  " ファイルパスを補完候補に追加
call dein#add('machakann/asyncomplete-ezfilter.vim')  " fuzzy な補完を実現

" ファイラ
call dein#add('lambdalisue/fern.vim')

" 入力UI拡張 (スモールプラグイン限定)
call dein#add('jiangmiao/auto-pairs') " 括弧やクォーテーションなどを補間
call dein#add('machakann/vim-sandwich') " 記号ペアで範囲を囲んだり置換や削除を行う
call dein#add('tomtom/tcomment_vim') " gcc でコメントアウト
call dein#add('junegunn/vim-easy-align') " 表示を整える
call dein#add('terryma/vim-expand-region') " vi) vi' などを使わずに登録キー連打でスコープ範囲を選択

" 表示UI拡張
call dein#add('nathanaelkane/vim-indent-guides') " インデントの可視化
call dein#add('machakann/vim-highlightedyank') " ヤンクのハイライト可視化
call dein#add('osyo-manga/vim-over') " 置換時にプレビュー表示(置換の基本コマンド: %s/before/after/g[c])
call dein#add('andymass/vim-matchup') " 構文のスコープ対応ジャンプ&ハイライトに対応するプラグイン

" 検索UI拡張
call dein#add('rhysd/accelerated-jk') " jk移動の高速化
call dein#add('rhysd/clever-f.vim') " f キージャンプの拡張
call dein#add('easymotion/vim-easymotion') " 特定文字へカーソルジャンプ
call dein#add('nelstrom/vim-visual-star-search') " ヴィジュアルモードの選択範囲を「*」で検索
call dein#add('osyo-manga/vim-anzu') " 検索位置を表示(マッチ数等も表示)
call dein#add('tpope/vim-fugitive') " vim上でgit操作を可能にする
call dein#add('airblade/vim-gitgutter') " git の diffを左端に表示
" TODO FZFの挙動が不審なので，いったん削除．後に追加予定．
if has('win32') || has('win64')
    call dein#add('junegunn/fzf') " 曖昧検索(ファジー検索)インターフェース
elseif has('mac') || has('unix')
    call dein#add('junegunn/fzf', {'build': './install --all'}) " 曖昧検索(ファジー検索)インターフェース
endif
call dein#add('junegunn/fzf.vim') " 'junegunn/fzf' のvim用プラグイン

" Colorscheme & Statusbar
call dein#add('rafi/awesome-vim-colorschemes') " カラースキーマ詰め合わせ
call dein#add('itchyny/lightline.vim') " ステータスライン(&タブライン)カスタマイズ 
call dein#add('mengelbrecht/lightline-bufferline') " ウィンドウ上部のタブを拡張

call dein#end()
" call dein#save_state()
" endif
""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""""""""""""""""""""""""""""""""""""""""""""""""
" プラグインが入っていなければvim起動時に自動でインストール
if dein#check_install()
    call dein#install()
endif
" filetype plugin on
filetype plugin indent on
syntax enable



"""""""""""""""""""""""""""""
" Language Server Protocol - コーディングサポート with python3
"""""""""""""""""""""""""""""
" vim-lsp 設定
let g:lsp_auto_enable = 1 " LSP 有効化
let g:lsp_diagnostics_enabled = 1 " Lint有効化
let g:lsp_signs_enabled = 1 " Lint結果を記号表示
let g:lsp_signs_priority = 10 " 記号の表示優先順位の設定
let g:lsp_diagnostics_echo_cursor = 1 " Lintの詳細をステータスラインに表示
let g:lsp_virtual_text_enabled = 0 " Lintの詳細を行末に表示
let g:lsp_preview_float = 1 " floating windowの利用
let g:lsp_highlights_enabled = 0 " ハイライト表示を無効化
let g:lsp_textprop_enabled = 0 " テキストプロパティの有効化
let g:lsp_preview_autoclose = 1 " プレビューを自動で閉じる
let g:lsp_preview_keep_focus = 1 " プレビューウィンドウに移動しない
" 左端記号の表示設定
let g:lsp_signs_error = {'text': 'X'}
let g:lsp_signs_warning = {'text': '!'}
let g:lsp_signs_information = {'text': 'i'}
let g:lsp_signs_hint = {'text': '?'}
" sign と virtual text の色定義 
augroup vimlsp_colors
    autocmd!
    au ColorScheme * :hi LspErrorText ctermfg=214 guifg=red
    au ColorScheme * :hi LspWarningText ctermfg=167 guifg=orange
    au ColorScheme * :hi LspInformationText ctermfg=gray guifg=gray
    au ColorScheme * :hi LspHintfomationText ctermfg=cyan guifg=cyan
augroup END

" vista.vim 設定
let g:vista_fzf_preview = ['right:50%'] " fzf の表示領域設定
let g:vista#renderer#enable_icon = 0 " アイコン表示を無効化
let g:vista#renderer#icons = {"function":"f", "variable":"v"} " アイコンの定義
let g:vista_default_executive = 'vim_lsp' " Vistaの実行プラグインを指定
let g:vista_executive_for = {'python':'vim_lsp'} " Vista の実行プラグインを言語別に指定可能
" Ctrl+vで表示/非表示をトグルで切り替える
nnoremap <silent> <C-v> :Vista!!<cr>

" """""""""""""""""""""""""""""
" 補完
" """""""""""""""""""""""""""""

" asyncomplete 設定
" popup表示までのち円時間[ms] (defaulat:30)
let g:asyncomplete_popup_delay = 10
" TAB で補完(順選択)
inoremap <expr> <Tab>   pumvisible() ? "\<C-n>" : "\<Tab>"
" Shift TAB で補完(逆順選択)
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"
" リターンキーで補完ポップアップを閉じる
inoremap <expr> <cr>    pumvisible() ? asyncomplete#close_popup() : "\<cr>"

" asyncompelte-buffer.vim 設定
call asyncomplete#register_source(asyncomplete#sources#buffer#get_source_options({
    \ 'name': 'buffer',
    \ 'allowlist': ['*'],
    \ 'priority': 10,
    \ 'blocklist': ['go'],
    \ 'completor': function('asyncomplete#sources#buffer#completor'),
    \ 'config': {
    \    'max_buffer_size': 5000000,
    \  },
    \ }))

" asyncomplete-file.vim 設定
au User asyncomplete_setup call asyncomplete#register_source(
    \ asyncomplete#sources#file#get_source_options({
    \ 'name': 'file',
    \ 'allowlist': ['*'],
    \ 'priority': 20,
    \ 'completor': function('asyncomplete#sources#file#completor')
    \ }))

" asyncompelte-ezfilter.vim 設定
" 大文字小文字を区別しないフィルタリング ver
let g:asyncomplete_preprocessor =
  \ [function('asyncomplete#preprocessor#ezfilter#filter')]
let g:asyncomplete#preprocessor#ezfilter#config = {}
let g:asyncomplete#preprocessor#ezfilter#config['*'] =
  \ {ctx, items -> ctx.filter(items)}

" neosnippet 設定
let g:neosnippet#enable_snipmate_compatibility = 1
let g:neosnippet#enable_completed_snippet = 1
let g:neosnippet#expand_word_boundary = 1
" 補完候補選択中に Ctrl+k でスニペットを展開
imap <expr><C-k> neosnippet#expandable_or_jumpable() ?"\<Plug>(neosnippet_expand_or_jump)" : ""
smap <expr><C-k> neosnippet#expandable_or_jumpable() ?"\<Plug>(neosnippet_expand_or_jump)" : ""

" echodoc 設定
let g:echodoc#enable_at_startup = 1
let g:echodoc#type = 'echo'

"""""""""""""""""""""""""""""
" Fern - ファイラ
"""""""""""""""""""""""""""""
" TODO ファイル編集のキーマップをdefxと揃えたいので，参照用にdefxの設定を残してある

" fern起動キーマップ(トグル型)
nnoremap <silent> <C-n> :Fern . -drawer -toggle<CR>

" キーマップ設定
function! s:fern_init() abort
  let g:fern#disable_viewer_hide_cursor = 1
  " 終了コマンド
  nnoremap <buffer> <silent> q :q<CR>
  nnoremap <buffer> <silent> <C-n> :q<CR>
  " カーソル位置のファイルの開き方
  map <buffer> <silent> <C-x> <Plug>(fern-action-open:split)
  map <buffer> <silent> <C-v> <Plug>(fern-action-open:vsplit)
  " カーソル行のファイルパス名をyank
  map <buffer> <silent> y <Plug>(fern-action-yank:path)
  " カーソル行のファイルをコピー
  map <buffer> <silent> c <Plug>(fern-action-copy)
  " カーソル行のファイルの移動元を指定
  map <buffer> <silent> m <Plug>(fern-action-move)
  " 新しいディレクトリの作成
  map <buffer> <silent> d <Plug>(fern-action-new-dir)
  " 新しいファイルの作成
  map <buffer> <silent> f <Plug>(fern-action-new-file)
  " ファイル(ディレクトリも)削除
  map <buffer> <silent> x <Plug>(fern-action-remove)
  " ドットファイルの表示/非表示切り替え
  map <buffer> <silent> h <Plug>(fern-action-hidden:toggle)
  " カーソル循環上下移動
  map <silent><buffer><expr> j line('.') == line('$') ? 'gg' : 'j'
  map <silent><buffer><expr> k line('.') == 1 ? 'G' : 'k'
"     " ファイル(ディレクトリ)の名前を変更
"     nnoremap <silent><buffer><expr> r defx#do_action('rename')
"     " 一つ上のディレクトリへ移動
"     nnoremap <silent><buffer><expr> u defx#do_action('cd', ['..'])
"     " ホームディレクトリへ移動
"     nnoremap <silent><buffer><expr> ~ defx#do_action('cd')
"     " vimのワーキングディレクトリを設定
"     nnoremap <silent><buffer><expr> w defx#do_action('change_vim_cwd')
"     " 複数選択
"     nnoremap <silent><buffer><expr> <Space> defx#do_action('toggle_select') . 'j'
"     " 全選択
"     nnoremap <silent><buffer><expr> * defx#do_action('toggle_select_all')
"     " 直前の操作を実行
"     nnoremap <silent><buffer><expr> . defx#do_action('repeat')
"     " 外部コマンド実行
"     nnoremap <silent><buffer><expr> e defx#do_action('execute_command')
"     " 再描画?
"     nnoremap <silent><buffer><expr> <C-l> defx#do_action('redraw')
"     " ステータスラインにフルパスを表示
"     nnoremap <silent><buffer><expr> <C-g> defx#do_action('print')
endfunction
augroup fern-setteings
  au!
  au FileType fern call s:fern_init()
augroup END


"""""""""""""""""""""""""""""
" 入力UI拡張 (スモールプラグイン限定)
"""""""""""""""""""""""""""""
" vim-easy-align 設定 [ga 入力後,指定の文字を入力して実行 (in visual mode)]
xmap ga <Plug>(EasyAlign)
nmap ga <Plug>(EasyAlign)

" vim-expand-region 設定
" キーマップ
vmap v <Plug>(expand_region_expand)
vmap V <Plug>(expand_region_shrink)
vmap <Space> y
" オブジェクト範囲の設定 / i:inner, w:word, b:brace, p:paragraph?
let g:expand_region_text_objects = {
            \ 'iw'  :0,
            \ 'iW'  :0,
            \ 'i"'  :1,
            \ 'i)'  :1,
            \ 'i}'  :1,
            \ 'i]'  :1,
            \ 'ib'  :1,
            \ 'iB'  :1,
            \ 'il'  :0,
            \ 'ip'  :0,
            \ 'ie'  :0,
            \ }


"""""""""""""""""""""""""""""
" 表示UI拡張
"""""""""""""""""""""""""""""
" vim-indent-guides 設定
let g:indent_guides_enable_on_vim_startup = 1 " indent guideの有効
let g:indent_guides_auto_colors = 0 " 自動配色の有効化
let g:indent_guides_guide_size = 1 " ガイドの幅を設定

" vim-highlightedyank 設定
highlight HighlightedyankRegion cterm=reverse gui=reverse 
let g:highlightedyank_highlight_duration = 500 " ハイライト表示する時間[ms]

" vim-over 設定
" Ctrl+s で起動 
nnoremap <C-s> :OverCommandLine <CR>%s//g<Left><Left>
" ノーマルモード時に sub と入力することでカーソル下の単語をハイライト付きで置換
nnoremap sub :OverCommandLine<CR>%s/<C-r><C-w>//g<Left><Left>
" ノーマルモード時に subp と入力することでコピーした文字列をハイライト付きで置換
nnoremap subp y:OverCommandLine<CR>%s!<C-r>=substitute(@0, '!', '\\!','g')<CR>!!gI<Left><Left><Left>

" vim-matchup 設定
let g:loaded_matchit = 1 " matchupの有効化
" スコープ対応の可視化色
augroup matchup_matchparen_highlight
    autocmd!
    autocmd ColorScheme * hi MatchParen ctermbg=bg guibg=bg ctermfg=yellow guifg=LightGreen cterm=bold gui=bold
augroup END


"""""""""""""""""""""""""""""
" 検索UI拡張
"""""""""""""""""""""""""""""
" accelerated-jk 設定
nmap j <Plug>(accelerated_jk_gj)
nmap k <Plug>(accelerated_jk_gk)

" clever-f.vim 設定
let g:clever_f_across_no_line = 0 " 検索対象が現在の行に無ければ次の行へ移動
let g:clever_f_smart_case = 1 " 小文字検索->大文字&小文字を対象 大文字検索->大文字のみ対象
let g:clever_f_use_migemo = 0 " 日本語とかの検索に対応(微妙に対応が悪いので無効化)

" vim-easymotion 設定
let g:EasyMotion_do_mapping = 0 " デフォルトキーマッピングの無効化
let g:EasyMotion_smartcase = 1 " 小文字入力時,大文字も含めて検索
let g:EasyMotion_keys = 'ASDFGHJKL' " ジャンプ先のターゲットキーを指定
let g:EasyMotion_use_upper = 1 " ターゲットキーを大文字で表示
" キーを入力後,任意の2文字を入力するとeasymotionを起動(分割ウィンドウに対応)
nmap ss <Plug>(easymotion-overwin-f2)
xmap ss <Plug>(easymotion-overwin-f2)
omap ss <Plug>(easymotion-overwin-f2)

" vim-anzu 設定
" ハイライト箇所の横に (n/MatchNum) 形式で表示
nmap n <Plug>(anzu-n)<Plug>(anzu-mode)
nmap N <Plug>(anzu-N)<Plug>(anzu-mode)

" vim-gitgutter 設定
set updatetime=100 " タイピング停止から反映されるまでの時間[ms]
let g:gitgutter_enabled = 1 " プラグインの有効化
let g:gitgutter_highlight_lines = 0 " 行のハイライトを無効化
let g:gitgutter_async = 1 " 非同期で処理 (非同期処理に対応していない場合機能しない)
let g:gitgutter_override_sign_column_highlight = 1 " 記号を表示するカラムの背景色変更しない
" 各記号の変更
let g:gitgutter_sign_added='+'
let g:gitgutter_sign_modified='~'
let g:gitgutter_sign_removed='-'
let g:gitgutter_sign_removed_first_line = '^'
let g:gitgutter_sign_modified_removed = 'w'
" ハンク(変更点のかたまり)へジャンプ
nmap <C-f> <Plug>(GitGutterNextHunk)
nmap <C-d> <Plug>(GitGutterPrevHunk)

" fzf 設定
" Windows 環境でripgrep を利用できるようにPATHを通す
let s:fzf_bin_dir = expand(g:dein_dir . '/repos/github.com/junegunn/fzf/bin')
" if has('win64')
let $PATH .= ';' . s:fzf_bin_dir
" else
" endif

" fzf.vim 設定
" <Leader> + a でRipgrep(ripgrepがインストールされていないと使えない)
nnoremap <Leader>a :Rg<CR>
" <Leader> + s で現在展開中のバッファの文字列を検索
nnoremap <Leader>s :BLines<CR>
" <Leader> + d で現在開いているバッファの一覧表示及び絞り込み
nnoremap <Leader>d :Buffers<CR>
" <Leader> + f でファイル検索
nnoremap <Leader>f :Files<CR>
" <Leader> + w マークの一覧表示
nnoremap <Leader>w :Marks<CR>
" <Leader> + q で最近展開したファイルを表示&検索
nnoremap <Leader>q :History<CR>
" [Insert] Ctrl + f パス保管用fzfを展開
imap <C-f> <plug>(fzf-complete-path)
" fzf ウィンドウ終了 (WSLで機能していないっぽい)
augroup fzf
    autocmd!
    autocmd! FileType fzf
    " fzfウィンドウ展開時，<Leader>を2回連続入力すると、ウィンドウを閉じる
    au FileType fzf tnoremap <Leader><Leader> <ESC>
augroup END
" 可能であれば展開済みバッファへ移動
let g:fzf_buffers_jump = 1
" 表示領域を画面下部N%以内にする
let g:fzf_layout = {'down': '~40%'}
" if has('mac') && has('unix')
"     " :Rg コマンドでプレビュー表示 (TODO windowsに対応できていない?)
"     command! -bang -nargs=* Rg
"                 \ call fzf#vim#grep(
"                 \   'rg --column --line-number --no-heading --color=always --smart-case '.shellescape(<q-args>), 1,
"                 \   <bang>0 ? fzf#vim#with_preview('up:~60%')
"                 \           : fzf#vim#with_preview('right:45%'),
"                 \   <bang>0)
"     " :Files コマンドでプレビュー表示
"     command! -bang -nargs=? -complete=dir Files
"                 \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)
" else
" endif
" ファイル展開方法
let g:fzf_action = {
            \ 'ctrl-t': 'tab split',
            \ 'ctrl-s': 'split',
            \ 'ctrl-v': 'vsplit',
            \ }


"""""""""""""""""""""""""""""
" Colorscheme & Statusbar
"""""""""""""""""""""""""""""
colorscheme iceberg
set bg=dark
set showtabline=2 " タブを常に表示

" ---------- lightline 設定 ---------- "
let g:lightline = {'separator': {'left': '', 'right': ''}, 'subseparator': {'left': '','right': ''}, 'mode_map': {'c': 'NORMAL'},}
" lightline.componet 定義
let g:lightline.component           = {}
let g:lightline.component.dir       = '%.35(%{expand("%:h:s?\\S$?\\0/?")}%)'
let g:lightline.component.winbufnum = '%n%{repeat(",", winnr())}%<'
let g:lightline.component.rows      = '%L'
let g:lightline.component.cd        = '%.35(%{fnamemodify(getcwd(), ":~")}%)'
let g:lightline.component.tabopts   = '%{&et?"et":""}%{&ts}:%{&sw}:%{&sts},%{&tw}'
let g:lightline.component.lineinfo  = '%l/%L'

" lightline.componet_function 定義
let g:lightline.component_function                = {}
let g:lightline.component_function.fugitive       = 'StlFugitive'
let g:lightline.component_function.cfi            = 'StlCurrentFuncInfo'
let g:lightline.component_function.currentfuncrow = 'StlCurrentFuncRow'
let g:lightline.component_function.modified       = 'LightlineModified'
let g:lightline.component_function.readonly       = 'LightlineReadonly'
let g:lightline.component_function.fugitive       = 'LightlineFugitive'
let g:lightline.component_function.filename       = 'LightlineFilename'
let g:lightline.component_function.fileformat     = 'LightlineFileformat'
let g:lightline.component_function.filetype       = 'LightlineFiletype'
let g:lightline.component_function.fileencoding   = 'LightlineFileencoding'
let g:lightline.component_function.mode           = 'LightlineMode'
let g:lightline.component_function.lsp            = 'LspServerStatusForStatusLine'

" lightline.component_expand 定義
let g:lightline.component_expand            = {}
let g:lightline.component_expand.buffers    = 'lightline#bufferline#buffers'
" let g:lightline.component_expand.lspwarning = 'LspWarningNum'
" let g:lightline.component_expand.lsperror   = 'LspErrorNum'

" lightline.component_type 定義
let g:lightline.component_type            = {}
let g:lightline.component_type.buffers    = 'tabsel'
" let g:lightline.component_type.lspwarning = 'warning'
" let g:lightline.component_type.lsperror   = 'error'
let g:lightline.component_type.lsp        = 'right'

" ステータスラインの表示フォーマット定義
" 動的表示用
let g:lightline.active         = {}
let g:lightline.active.left    = [['mode', 'paste'], ['fugitive'], ['filename', 'readonly']]
let g:lightline.active.right   = [['lsperror'], ['lspwarning'], ['lsp'], ['lineinfo']]
" 静的表示用
let g:lightline.inactive       = {}
let g:lightline.inactive.left  = [['mode', 'paste'], ['fugitive'], ['filename', 'readonly']]
let g:lightline.inactive.right = [['lineinfo']]
" タブラインの表示フォーマット定義
let g:lightline.tabline = {}
let g:lightline.tabline = {'left': [ ['buffers'] ], 'right': [ ['fileformat'], ['fileencoding'], ['filetype'], ['cd'] ]}

" ---------- component 定義 ---------- "
function! LightlineModified()
    return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction
function! LightlineReadonly()
    return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? 'x' : ''
endfunction
function! LightlineFilename()
    return ('' != LightlineReadonly() ? LightlineReadonly() . ' ' : '') .
                \ ('' != expand('%:t') ? expand('%:t') : '[No Name]') .
                \ ('' != LightlineModified() ? ' ' . LightlineModified() : '')
endfunction
function! LightlineFugitive()
    if &ft !~? 'vimfiler\|gundo' && exists('*fugitive#head')
        return fugitive#head()
    else
        return ''
    endif
endfunction
function! LightlineFileformat()
    return winwidth(0) > 70 ? &fileformat : ''
endfunction
function! LightlineFiletype()
    return winwidth(0) > 70 ? (&filetype !=# '' ? &filetype : 'no ft') : ''
endfunction
function! LightlineFileencoding()
    return winwidth(0) > 70 ? (&fenc !=# '' ? &fenc : &enc) : ''
endfunction
function! LightlineMode()
    return winwidth(0) > 40 ? lightline#mode() : ''
endfunction
function! LspServerStatusForStatusLine()
    return g:lsp#get_server_status()
endfunction
function! LspWarningNum()
    let s:warning_num = lsp#get_buffer_diagnostics_counts()['warning']
    return s:warning_num == 0 ? '' : 'Warning:' . s:warning_num
endfunction
function! LspErrorNum()
    let s:error_num = lsp#get_buffer_diagnostics_counts()['error']
    return s:error_num == 0 ? '' : 'Error:' . s:error_num
endfunction

" ---------- lightline カラー定義 (iceberg仕様) ---------- "
" initialization
command! -bar LightLineUpdate call lightline#init()| call lightline#colorscheme()| call lightline#update()

let g:lightline.colorscheme = 'mypalette'
let g:pltt = {'inactive': {}, 'normal': {}, 'insert': {}, 'visual': {}, 'replace': {}, 'tabline': {}}

" let s:hoge = [ {guifg}, {guibg}, {ctermfg}, {ctermbg} ]
let s:base_0 = ['#c6c8d1', '#1e2132', 252, 235]  " basic
let s:base_1 = ['#1e2132', '#c6c8d1', 235, 252]  " basic reverse
let s:base_2 = ['#c6c8d1', '#1e2132', 252, 235]  " tabline

" mode color
let s:mode_stl = ['#e27878', '#1e2132', 203, 235]
let s:mode_nor = ['#282828', '#84a0c6', 235, 110]
let s:mode_ins = ['#282828', '#a093c7', 235, 140]
let s:mode_vis = ['#282828', '#e2a478', 235, 216]
let s:mode_rep = ['#282828', '#818596', 235, 234]
let s:mode_war = ['#e2a478', '#1e2132', 216, 236]
let s:mode_err = ['#e27878', '#1e2132', 203, 236]

" tabline
let s:tab_f = ['#a093c7', '#161821', 140, 234]
let s:tab_b = ['#c6c8d1', '#1e2132', 252, 235]

" statusline basic
let g:STL_BASECOLOR = s:base_0
" statusline attr
let g:STL_ATTRIBUTECOLOR = s:base_1
" statusline
let g:pltt.inactive.middle = [g:STL_BASECOLOR]
let g:pltt.inactive.left = [s:mode_stl, ]
let g:pltt.inactive.right = [g:STL_BASECOLOR]
" normal mode
let g:pltt.normal.middle = [g:STL_BASECOLOR]
let g:pltt.normal.left = [s:mode_nor, ]
let g:pltt.normal.right = deepcopy(g:pltt.inactive.right)
" insert mode
let g:pltt.insert.middle = [g:STL_BASECOLOR]
let g:pltt.insert.left = [s:mode_ins, ]
let g:pltt.insert.right = deepcopy(g:pltt.inactive.right)
" visual mode
let g:pltt.visual.middle = [g:STL_BASECOLOR]
let g:pltt.visual.left = [s:mode_vis, ]
let g:pltt.visual.right = deepcopy(g:pltt.inactive.right)
" replace mode
let g:pltt.replace.middle = [g:STL_BASECOLOR]
let g:pltt.replace.left = [s:mode_rep, ]
let g:pltt.replace.right = deepcopy(g:pltt.inactive.right)
" tabline
let g:pltt.tabline.middle = [s:base_2]
let g:pltt.tabline.left = [s:tab_b]
let g:pltt.tabline.right = [s:base_2, ]
let g:pltt.tabline.tabsel = [s:tab_f]  " active window color
" error and warning
let g:pltt.normal.warning = [s:mode_war]
let g:pltt.normal.error   = [s:mode_err]
" update palette
let g:lightline#colorscheme#mypalette#palette = g:pltt
unlet g:pltt g:STL_BASECOLOR g:STL_ATTRIBUTECOLOR

" lightline-bufferline 設定
let g:lightline#bufferline#show_number = 0 " バッファ名に番号を表示しない
let g:lightline#bufferline#shorten_path = 1 " バッファ名を表示するときにパスを省略型で表示
let g:lightline#bufferline#unnamed = '[E]' " 名前の付いていないバッファが表示された時の標記方法
let g:lightline#bufferline#filename_modifier = '%f' " バッファの表示設定(相対パスで表示)


""""""""""""""""""""""""""""""""""""""""""""""""""""""
" GVim用設定ファイル(ターミナルからvim.exeを起動するとおかしくなるので一部コメント化)
""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has('win32')  && has('gui')
    " Screen UI 設定(余計なGUIを削除)
    " set lines=30 " ウインドウの高さ
    " set columns=100 " ウインドウの幅
    set cmdheight=1 " コマンドラインの高さ(GUI使用時)
    set guioptions-=e " タブバーCUI化
    set guioptions-=T " ツールバー非表示
    set guioptions-=m " メニューバー非表示
    " スクロールバー非表示(左右/水平)
    set guioptions-=r
    set guioptions-=R
    set guioptions-=l
    set guioptions-=L
    set guioptions-=b
    set guioptions-=h

    " Mouse UX 設定
    set mouse=a " どのモードでもマウスを使えるようにする
    set nomousefocus " マウスの移動でフォーカスを自動的に切替えない (mousefocus:切り替える)
    set mousehide " 入力時にマウスポインタを隠す (nomousehide:隠さない)
    " set guioptions+=a " ビジュアル選択(D&D他)を自動的にクリップボードへ (:help guioptions_a)

    " Input for japanese 設定
    if has('multi_byte_ime') || has('xim') " IME ON時のカーソルの色を設定(設定例:紫)
        highlight CursorIM guibg=Purple guifg=White
        set iminsert=0 imsearch=0 " 挿入モード・検索モードでのデフォルトのIME状態設定 (8.0.1114 でデフォルトになったが念のため残してある)
        " 挿入モードでのIME状態を記憶させない場合、次行のコメントを解除
        inoremap <silent> <ESC> <ESC>:set iminsert=0<CR>
    endif

    " Font 設定
    " フォント確認 : Win+R実行後, %windir%\fonts を入力
    " Cica[https://github.com/miiton/Cica/releases]などがオススメ
    set guifont=Cica-Regular:h16:cSHIFTJIS  " 使わなくても良いかも(くどい)
    " set guifont=MS_Gothic:h12:cSHIFTJIS  " Default value
    set linespace=1 " 行間隔の設定
    " 一部のUCS文字の幅を自動計測して決める
    if has('kaoriya')
        set ambiwidth=auto
    endif

endif
